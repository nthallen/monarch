#! /usr/bin/perl -w
use strict;

my %ID;
my %IDfound;
load_id();

open(my $lsusbt, '-|', "lsusb -tvv") or
  die "Cannot run lsusb -tvv\n";

my %devnum;
my $curdevnum = '';
while (my $line = <$lsusbt>) {
  if ($line =~ m/ Port \d+: Dev (\d+),/) {
    $curdevnum = $1;
  } elsif ($line =~ m|/sys/bus/usb/devices/([^ ]+) |) {
    $devnum{$1} = $curdevnum;
  }
}
close($lsusbt);

print "# usb_enum: ", scalar(localtime()), "\n";

my @serdevs = ( </sys/bus/usb/devices/*:*/tty?*>,
                </sys/bus/usb/devices/*:*/tty/tty?*>);
# print "Devices:\n", map("  $_\n", @serdevs);
for my $devstr (@serdevs) {
  $devstr =~ m|^/sys/bus/usb/devices/([^:]+):([^/]+)/(?:[^/]+/)?(.*)$| ||
    die "Pattern did not match\n";
  my $usbt = $1;
  my $if = $2;
  my $port = "/dev/$3";
  $usbt =~ m/^(\d+)-/ || die "Unexpected usbt for devstr '$devstr'\n";
  my $bus = $1;
  my $devnum = $devnum{$usbt};
  open(my $lsusb, '-|', "lsusb -s$bus:$devnum -v 2>/dev/null | grep iSerial") or
    die "Unable to open lsusb pipe\n";
  my $line = <$lsusb> || '';
  close($lsusb) or print "# Error closing lsusb -s$bus:$devnum pipe for $port\n";
  chomp $line;
  if ( $line =~ m/iSerial \s+ \d+ \s+ (\S+)$/x ) {
    my $SN = $1;
    if ($ID{"$SN:$if"}) {
      print $ID{"$SN:$if"}, "_port=$port\n";
      $IDfound{"$SN:$if"} = 1;
    } else {
      print "# Unidentified board found, SN $SN:$if, devnum $devnum on $port\n";
    }
  } else {
    print "# Unable to identify iSerial for devnum $devnum on $port: '$line'\n";
  }
}
    
for my $snif (keys(%ID)) {
  print "$ID{$snif}_port=''\n" unless $IDfound{$snif};
}

sub load_id {
  my @idpath = qw(.);
  defined($ENV{TMBINDIR}) &&
    unshift(@idpath, $ENV{TMBINDIR});
  while (@idpath) {
    my $dir = pop(@idpath);
    my $path = "$dir/USB.id";
    if ( -f $path ) {
      load_id_path($path);
      return;
    }
  }
}

sub load_id_path {
  my $path = shift(@_);
  open(my $idfile, '<', $path) ||
    warn "usb_enum: Unable to open '$path'\n";
  while (my $line = <$idfile>) {
    chomp $line;
    next if $line =~ m/^\s*(?:#.*)?$/;
    if ($line =~ m/^ \s* ([\w_]+) \s+ ([a-z0-9]+) ((?::\d+\.\d+)?) \s* $/ix) {
      my $mnc = $1;
      my $sn = $2;
      my $if = $3 || ":1.0";
      $ID{"$sn$if"} = $mnc;
    }
  }
}
